Programa        = { Decl | Cmd } ;

Decl            = "var" Ident [ ":" Tipo ] [ "=" Expr ] ";" ;
Tipo            = "int" | "bool" | "texto" ;

Cmd             = Bloco
                | Atrib ";"
                | Se
                | Enquanto
                | Para
                | Escreva ";"
                | Leia ";"
                | ";"
                ;

Bloco           = "{" { Decl | Cmd } "}" ;

Atrib           = Ident "=" Expr ;

Se              = "se" "(" Expr ")" Cmd [ "senao" Cmd ] ;

Enquanto        = "enquanto" "(" Expr ")" Cmd ;

Para            = "para" "(" ParaInitOpt ";" ParaCondOpt ";" ParaUpdOpt ")" Cmd ;
ParaInitOpt     = [ Atrib | Decl ] ;
ParaCondOpt     = [ Expr ] ;
ParaUpdOpt      = [ Atrib ] ;

Escreva         = "escreva" "(" ArgListOpt ")" ;
Leia            = "leia" "(" Ident ")" ;

ArgListOpt      = [ ArgList ] ;
ArgList         = Expr { "," Expr } ;

Expr            = Or ;
Or              = And { "||" And } ;
And             = Eq { "&&" Eq } ;
Eq              = Rel { ( "==" | "!=" ) Rel } ;
Rel             = Add { ( "<" | "<=" | ">" | ">=" ) Add } ;
Add             = Mul { ( "+" | "-" ) Mul } ;
Mul             = Un  { ( "*" | "/" | "%" ) Un } ;
Un              = ( "!" | "-" ) Un | Prim ;

Prim            = Lit | Ident | "(" Expr ")" ;

Lit             = IntLit | BoolLit | StrLit ;
IntLit          = Digito { Digito } ;
BoolLit         = "verdadeiro" | "falso" ;
StrLit          = "\"" { StrChar } "\"" ;

Ident           = Letra { Letra | Digito | "_" } ;

Digito          = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;
Letra           = "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M"
                | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"
                | "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m"
                | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" ;

StrChar         = " " | "!" | "#" | "$" | "%" | "&" | "'" | "(" | ")" | "*" | "+" | "," | "-"
                | "." | "/" | ":" | ";" | "<" | "=" | ">" | "?" | "@"
                | "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
                | "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M"
                | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"
                | "[" | "\\" | "]" | "^" | "_" | "`"
                | "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m"
                | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z"
                | "{" | "|" | "}" | "~" ;
